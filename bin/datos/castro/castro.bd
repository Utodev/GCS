; -------------------------------------------------------------
;   EL SECRETO DE CASTRONEGRO
;   (c)1995 JSJ Soft Ltd.
;   Aventura escrita para el sistema S.I.N.T.A.C. G3
; -------------------------------------------------------------

;--------------------------------------------------------------
; Variables usadas:
;        21 - di†logo con Markus
;       252 a 255 - auxiliares
;--------------------------------------------------------------
; Banderas usadas:
;        21 - 0:bedel no hace caso, 1:bedel hace caso
;        22 - 1:bedel contest¢ sobre William Godfrey
;       255 - auxiliar
;--------------------------------------------------------------

; definici¢n de constantes
\\V_MOV         14      ; m†ximo nß para verbos de movimiento
\\N_CONV        20      ; m†ximo nß para nombres convertibles
\\N_PROP        50      ; m†ximo nß para nombres propios

; localidades especiales
\\LOC_NOCREADO  252     ; localidad para objetos no creados
\\LOC_PUESTO    253     ; localidad para objetos puestos (WEAR)
\\LOC_COGIDO    254     ; localidad para objetos cogidos (GET)
\\LOC_ACTUAL    255     ; equivale a localidad actual

; n£mero de palabra no v†lida
\\PAL_NO        255

; c¢digos de INPUT
\\TECLA_F1      255     ; tecla F1
\\TECLA_F2      254     ; tecla F2
\\TECLA_F3      253     ; tecla F3
\\TECLA_F4      252     ; tecla F4
\\TECLA_F5      251     ; tecla F5
\\TECLA_F6      250     ; tecla F6
\\TECLA_F7      249     ; tecla F7
\\TECLA_F8      248     ; tecla F8
\\TECLA_F9      247     ; tecla F9
\\TECLA_F10     246     ; tecla F10
\\BTN_RATON     238     ; pulsaci¢n bot¢n del rat¢n

; c¢digos para WBORDER
\\WB_NO         0       ; si borde
\\WB_SENCILLO   1       ; sencillo
\\WB_DOBLE      2       ; doble
\\WB_DOBLE_SENC 3       ; doble-sencillo laterales
\\WB_SENC_DOBLE 4       ; sencillo-doble laterales
\\WB_SOMBR1     5       ; sombreado 1
\\WB_SOMBR2     6       ; sombreado 2
\\WB_SOMBR3     7       ; sombreado 3
\\WB_RELLENO    8       ; relleno

; valores para SETAT, CLEARAT, HASAT, HASNAT
\\AT_PRENDA     16      ; atributo de prenda
\\AT_LUZ        17      ; atributo de fuente de luz

; valores para DEBUG
\\DEBUG_SI      1       ; activa paso a paso
\\DEBUG_NO      0       ; desactiva paso a paso

; valores para TIME
\\TIME_FRASE    0       ; tiempo para teclear toda la frase
\\TIME_LETRA    1       ; tiempo para teclear primera letra

; modos de pantalla (MODE)
\\MODO_16       0       ; modo de 640x480, 16 colores
\\MODO_256      1       ; modo de 320x200, 256 colores

; modos de funcionamiento de GRAPHIC
\\GRF_PRESENTA  0       ; dibuja el gr†fico seg£n se va cargando
\\GRF_MEM       1       ; dibuja el gr†fico una vez se ha cargado
\\GRF_PRESENTAP 2       ; como GRF_PRESENTA pero no modifica la paleta
\\GRF_MEMP      3       ; como GRF_MEM pero no modifica la paleta

; valores para MOUSE
\\RATON_SI      1       ; activa rat¢n
\\RATON_NO      0       ; desactiva rat¢n

; valores para MOUSEBT
\\BTN_IZQUIERDO 0       ; comprueba bot¢n izquierdo
\\BTN_DERECHO   1       ; comprueba bot¢n derecho
\\BTN_IZQDER    2       ; comprueba ambos a la vez
\\BTN_BOTON     3       ; comprueba cualquiera de los dos

; valores para MUSIC
\\MUS_1         0       ; toca la melod°a una sola vez
\\MUS_REP       1       ; repite la melod°a c°clicamente

; valores para PLAYFLI
\\FLI_REP       0       ; repite la animaci¢n c°clicamente
\\FLI_1         1       ; ejecuta la animaci¢n una sola vez
;--------------------------------------------------------------
\\FONDO_VTXT    75      ; color de fondo de ventana de texto
\\PPLANO_VTXT   78      ;   "   "  primer plano de ventana de texto
\\FONDO_VGRF    75      ;   "   "  fondo de ventana de gr†ficos
\\PPLANO_VGRF   0       ;   "   "  primer plano de ventana de gr†ficos
\\FONDO_VBRUJ   75      ;   "   "  fondo de ventana de br£jula
\\PPLANO_VBRUJ0 76      ;   "   "  primer plano br£jula dir. desactivada
\\PPLANO_VBRUJ1 50      ;   "   "  primer plano br£jula dir. activada
\\PPLANO_DIALOG 77      ;   "   "  texto de di†logo
\MSY
@0 Has cogido _.|@
@1 Aqu° no est† eso.|@
@2 No puedes coger _. Llevas demasiadas cosas.|@
@3 Ya tienes eso.|@
@4 Dejas _.|@
@5 No tienes eso.|@
@6 |> @         ; PROMPT
@7 _@           ; CURSOR
@8 No puedes hacer eso.|@
@9 TambiÇn puedes ver: @
@10 nada.|@
@11 , @
@12  y @
@13 .|@
@14 Tienes: @
@15 Llevas puesto: @
@16 Ya llevas puesto _.|@
@17 No puedes ponerte _.|@
@18 Te pones _.|@
@19 No llevas puesto eso.|@
@20 Te quitas _.|@
@21 No puedes ir en esa direcci¢n.|@
@22 --- Pulsa una tecla ---@
@23 Est† oscuro. No puedes ver nada.@
@24 ®Est†s seguro? @
@25 SN@
@26 Nombre del fichero: @
@27 Fichero ya existe. ®Quieres continuar? @
@28 Error de apertura de fichero.|@
@29 Error de entrada/salida en fichero.|@
@30 Fichero no v†lido.|@
@31 ®Lo intentas de nuevo? @
\END
\VOC
; ------------------------
;  Palabras de movimiento
; ------------------------
N               1       nombre
NORTE           1       nombre
S               2       nombre
SUR             2       nombre
E               3       nombre
ESTE            3       nombre
O               4       nombre
OESTE           4       nombre
NE              5       nombre
NORESTe         5       nombre
NO              6       nombre
NOROESte        6       nombre
SE              7       nombre
SURESTe         7       nombre
SO              8       nombre
SUROESte        8       nombre
ARRIBA          9       nombre
SUBIR           9       verbo
SUBE            9       verbo
SUBO            9       verbo
BAJAR           10      verbo
BAJA            10      verbo
BAJO            10      verbo
ABAJO           10      nombre
ENTRAR          11      verbo
ENTRA           11      verbo
ENTRO           11      verbo
SALIR           12      verbo
SALGO           12      verbo
SAL             12      verbo
; --------
;  Verbos
; --------
; NOTA: si un verbo tiene menos de 6 letras debes a§adir las correspon-
; dientes para el verbo con terminaci¢n. Ej.: para DEJA tambiÇn hay que
; a§adir en el vocabulario DEJALO, DEJALA, DEJALE.

COGER           20      verbo   ; Coger
COGERLo         20      verbo
COGE            20      verbo
COGELO          20      verbo
COGELA          20      verbo
COGELE          20      verbo
COJO            20      verbo

DEJAR           21      verbo   ; Dejar
DEJARLo         21      verbo
DEJA            21      verbo
DEJALO          21      verbo
DEJALA          21      verbo
DEJALE          21      verbo
DEJO            21      verbo

PONER           22      verbo   ; Poner
PONERLo         22      verbo
PONTE           22      verbo
PONTELo         22      verbo
PONGO           22      verbo
PON             22      verbo
PONLO           22      verbo
PONLA           22      verbo
PONLE           22      verbo
PONSELo         22      verbo
PONME           22      verbo
PONMELo         22      verbo

QUITAR          23      verbo   ; Quitar (prendas)
QUITA           23      verbo
QUITALo         23      verbo
QUITO           23      verbo

M               24      verbo   ; Mirar, describir
MIRAR           24      verbo
MIRARLo         24      verbo
MIRA            24      verbo
MIRALO          24      verbo
MIRALA          24      verbo
MIRALE          24      verbo
MIRO            24      verbo
DESCRIbir       24      verbo

FIN             25      verbo   ; Fin
ACABAR          25      verbo
ACABA           25      verbo
ACABO           25      verbo
TERMINar        25      verbo

EX              26      verbo   ; Examinar
EXAMINar        26      verbo
LEER            26      verbo   ; Leer
LEERLO          26      verbo
LEERLA          26      verbo
LEERLE          26      verbo
LEO             26      verbo
LEE             26      verbo
LEELO           26      verbo
LEELA           26      verbo
LEELE           26      verbo

SAVE            27      verbo   ; Grabar
GRABAR          27      verbo
GRABA           27      verbo
GRABO           27      verbo

LOAD            28      verbo   ; Cargar
CARGAR          28      verbo
CARGA           28      verbo
CARGO           28      verbo

DECIR           29      verbo   ; Decir, hablar, preguntar
DECIRLe         29      verbo
DIGO            29      verbo
DI              29      verbo
DILE            29      verbo
HABLAR          29      verbo
HABLA           29      verbo
PREGUNtar       29      verbo

ABRIR           30      verbo   ; Abrir
ABRIRLo         30      verbo
ABRO            30      verbo
ABRE            30      verbo
ABRELO          30      verbo
ABRELA          30      verbo
ABRELE          30      verbo

CERRAR          31      verbo   ; Cerrar
CIERRO          31      verbo
CIERRA          31      verbo

LUCHO           32      verbo   ; Luchar
LUCHAR          32      verbo
LUCHA           32      verbo
MATAR           32      verbo   ; Matar
MATARLe         32      verbo
MATA            32      verbo
MATALE          32      verbo
MATO            32      verbo
GOLPEAr         32      verbo   ; Golpear
ATACAR          32      verbo   ; Atacar
ATACA           32      verbo
ATACALe         32      verbo
ATACO           32      verbo
PEGAR           32      verbo
PEGO            32      verbo
PEGA            32      verbo
PEGALE          32      verbo

TOCAR           33      verbo
TOCARLo         33      verbo

; ---------
;  Nombres
; ---------

; convertibles (pueden usarse como verbos)
I               15      verbo   ; Inventario
INVENTario      15      verbo
RATON           16      verbo

; propios (no les afectan las ternimaciones LO, LA, LE)
TODO            20      nombre
BEDEL           21      nombre
WILLIAm         22      nombre
GODFREy         22      nombre
MARKUS          23      nombre

; otros nombres
RAM             50      nombre  ; para SAVE RAM y LOAD RAM
PERIODico       51      nombre
MESA            52      nombre
CAMPANilla      53      nombre
HOLA            54      nombre
PUERTA          55      nombre
SI              56      nombre

; -----------
;  Adjetivos
; -----------

; --------------
;  Conjunciones
; --------------
Y               1       conjunci¢n
LUEGO           1       conjunci¢n
\END
\MSG 0
; mensajes generales

@0 El viejo bedel lleva unas peque§as gafas redondas que caen sobre su
 prominente nariz.@
@1 Ensimismado en la lectura, el bedel no te hace ni caso.@
@2 Sobre la mesa de madera hay una peque§a campanilla.@
@3 INVENTARIO@
@4 CARGAR@
@5 GRABAR@
@6 SALIR@
@7 NORTE@
@8 SUR@
@9 ESTE@
@10 OESTE@
@11 NORESTE@
@12 NOROESTE@
@13 SURESTE@
@14 SUROESTE@
@15 De madera y con manillas de lat¢n.@
@16 En una de ellas ves un letrero en el que est† escrito: 'Dr. Markus'.@
@17 ®D¢nde quieres entrar?.@
@18 El Doctor Markus est† aqu°.@
@254 (c)1995 JSJ Soft Ltd.@
\END
\MSG 1
; descripciones de objetos

@0 No ves nada especial.|@
\END
\MSG 2
; di†logo con PSIs

@0 : @          ; tras el nombre del PSI que habla
@1 Aqu° nadie te hace ni caso.|@
@2 No entiendo lo que quiere decir.|@
@3 Bedel@
@4 ≠Vale!, ≠vale! que no estoy sordo, ®sabe j¢ven?. ®QuÇ desea?.@
@5 ≠Que no estoy sordo!, ≠le§e!.@
@6 Hola.@
@7 El Doctor William Godfrey dice... No sÇ, quiz† el Doctor Markus pueda
 ayudarle. Busque en el primer piso. Probablemente estÇ en su despacho.@
@8 William Godfrey... no le conozco, ≠dÇjeme en paz de una vez!.@
@9 Markus@
@10 Por favor, p¢ngase c¢modo. ®QuÇ desea?@
@11 ®William?. ®Le ocurre algo?@
@12 William es un gran amigo m°o. Hace semanas que no le veo, desde que se fuÇ
 de viaje a Silver City. Al parecer quer°a continuar con sus estudios sobre
 las supersticiones de los Apaches y los antiguos habitantes espa§oles del
 territorio. ®Cree que est† en peligro?.@
@13 Creo que ya le he dicho todo lo que sÇ de William.@
@14 Menos mal ya que hace semanas que no le veo.@
@15 Me empiezan a molestar sus preguntas. ®Le pasa algo a William?.@
@16 De todas formas si le ve dÇle recuerdos m°os.@
@17 Entonces espero que le encuentre pronto antes de que se meta en alg£n l°o.@
\END
\MSG 3
; peri¢dico

;  EXTRA•AS DESAPARICIONES DESCONCIERTAN A LOS SHERIFFS
@0 ‰¯ÙÚ‡Ó‡Û „‰Û‡‡ÚË‚ËÔÌ‰Û „‰Û‚ÔÌ‚Ë‰ÚÙ‡Ì ‡ ÎÔÛ ÛÁ‰ÚËÂÂÛ@
@1    La polic°a del Sudoeste de Nuevo MÇxico est† perpleja ante una serie de
 desapariciones de personas, evidentemente inconexas entre s°.|
   El 28 de Septiembre se descubri¢ que Joaqu°n Villeira-Pereira, nativo de la
 peque§a localidad de Castronegro, hab°a abandonado su habitaci¢n en el Hotel
 Armitage de Silver City, dejando all° todas sus posesiones. Las autoridades
 suponen que existen indicios de criminalidad, pero no existen sospechosos ni
 pistas.|
   Esto no ser°a excesivamente sorprendente si no fuera por dos desapariciones
 previas. El 16 de Septiembre desapareci¢ el doctor William Godfrey, conocido
 profesor de Psicolog°a de la Universidad de Nuevo MÇxico en Alburquerque, en
 el camino entre Silver City y Las Cruces. El 18, un viajero de Boston, el
 se§or David Lane, desapareci¢ de la habitaci¢n que hab°a alquilado en el
 centro de Silver City.|
   Ambas desapariciones est†n a£n por resolver, aunque la polic°a cree que
 podr°an estar relacionadas.@
;  CONTINUA EL MISTERIO DE LAS MUERTES DE GANADO
@2 ‚ÔÌÙËÌı‡ ‰Î ÏËÛÙ‰ÚËÔ „‰ Î‡Û Ïı‰ÚÙ‰Û „‰ Ê‡Ì‡„Ô@
@3    En el cap°tulo m†s reciente hasta ahora de las misteriosas muertes de
 ganado en Nuevo MÇxico, el ranchero Charles Dexter perdi¢ 92 cabezas de
 ganado entre el 8 y el 19 de Septiembre, en su rancho de la aislada localidad
 de Flourine. Esta ha sido la mayor pÇrdida atribuida a los, hasta ahora
 inaprehensibles, 'asesinos de ganado'. Las reses fueron halladas muertas en
 los alrededores del rancho del se§or Dexter, todas ellas con la lengua
 arrancada de cuajo, y tambiÇn con otros signos de violencia.|
   El se§or Dexter ha quedado tan afectado por el terrible suceso que ha vendido
 su rancho, emigrando a Europa. Antes de marchar manifest¢: 'Tengo una cierta
 idea de quiÇnes han estado matando mi ganado. No soy cobarde, pero no tengo
 deseo alguno de enfrentarme con ellos. Me voy para siempre.'.|
   Durante los £ltimos dos a§os han tenido lugar misteriosas mutilaciones de
 ganado en los ranchos locales, pero por regla general s¢lo se ve°an afectadas
 una o dos vacas al mes. El nuevo incidente parece apuntar a un posible
 recrudecimiento de estos extra§os sucesos en el Sudoeste de los EEUU.@
\END
\LOC
@0 Los muebles del despacho acumulan ya polvo de varios d°as. Tu escritorio
 se encuentra lleno de papeles y facturas sin pagar. Algunos viejos
 recortes de peri¢dicos te recuerdan aquellos dif°ciles casos resueltos con
 la sagacidad propia de un buen detective privado.@
# SUR 1

@1 Las calles de la ciudad de Alburquerque est†n plagadas de gente que
 se dirige a sus quehaceres diarios. Hacia el norte el edificio de oficinas
 donde tienes instalado tu despacho. Hacia el sur la calle desemboca en una
 gran avenida.@
# NORTE 0
# SUR 2

@2 Un edificio destaca en la amplia avenida por la que caminas. La Universidad
 de Nuevo MÇjico abre sus puertas a estudiantes, profesores e investigadores
 en numerosos temas del conocimiento.@
# NORTE 1
# ESTE 3

@3 El interior de la Universidad es suntuoso. Los pasos resuenan en sus pulidos
 suelos de m†rmol. Una amplia escalinata conduce al piso superior. A tu
 izquierda un viejo bedel descansa leyendo el peri¢dico tras una mesa de
 madera.@
# OESTE 2
# SUBIR 4

@4 La escalinata se abre paso hasta un amplio pasillo. A la izquierda unos
 amplios ventanales dan a la calle principal de Alburquerque. A la derecha
 una serie de puertas de madera que acusan ya el paso del tiempo.@
# BAJAR 3

@5 El despacho tiene una decoraci¢n muy cl†sica. Cientos de libros se acumulan
 en las estanter°as de madera.@
# SALIR 4
\END
\OBJ
; Banderas usuario ƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒƒø
; Tipo (Prenda, Luz) ƒƒƒƒƒƒƒƒƒƒƒø               ≥
; Loc. inicio ƒƒƒƒƒƒƒƒƒƒø       ≥               ≥
;   252 - no creado     ≥       ≥               ≥
;   253 - puesto        ≥       ≥               ≥
;   254 - cogido        ≥       ≥               ≥
; Adjetivo ƒƒƒƒƒƒƒø     ≥       ≥               ≥
; Nombre ø        ≥     ≥       ≥               ≥

@0 PERIODICO      _     0       L               OOOOOOOOOOOOOOOO
un peri¢dico
\END
\PRO 0
; se ejecuta tras cada inicializaci¢n y tras cada condacto DESC

_       _       PROCESS 4       ; inicializaciones (no tras un condacto DESC)

_       _       NOTZERO 2       ; si hay que saltar acciones DESC
                                ; (ej, se ejecut¢ RESTART)
                SKIP $buc_princ
_       _       SET 2           ; aqu° llega si es inicio del juego o DESC

_       _       PROCESS 10      ; dibuja gr†fico de localidad actual

; ejecuta diversas acciones tras una descripci¢n.

_       _       NORESP
_       _       NEWLINE
                LISTOBJ         ; lista objetos
                PROCESS 1       ; el Proceso 1 se ejecuta tras una descripci¢n
$buc_princ
_       _       TIMEOUT
                PROCESS 3
_       _       SYSMESS 6
                INPUT           ; pide frase de entrada
                PROCESS 5       ; procesa teclas de funci¢n
                SKIP $buc_princ
_       _       NEWLINE
$buc_sec
_       _       PROCESS 2       ; analiza, salta a sgte. entr. si no hay m†s
                NORESP
                PROCESS 3       ; ejecuta despuÇs de respuesta
                SKIP $buc_sec

_       _       SKIP $buc_princ
\END
\PRO 1
; se ejecuta tras una descripci¢n

_       _       PROCESS 6       ; imprime salidas de la localidad actual

_       _       AT 5
                MESSAGE 0 18
\END
\PRO 2
; tabla de Respuestas

_       _       PARSE
                NOTDONE         ; no hay m†s

_       _       RESP

ENTRAR  _       AT 4
                ZERO 22
                MESSAGE 0 17
                DONE

ENTRAR  _       AT 4
                GOTO 5
                CLW [0]
                DESC [1]

_       _       ISMOV           ; si es movimiento
                MOVE 1          ; intenta mover en esa direcci¢n
                CLW [0]         ; si pudo moverse, borra ventana activa
                DESC [1]        ; y describe localidad
_       _       ISMOV
                SYSMESS 21      ; si no pudo moverse
                DONE

I       _       SET 7           ; este SET es necesario s¢lo si
                SYSMESS 14      ; LISTAT y LISTOBJ est†n en modo 0
                LISTAT 254
                SYSMESS 15
                LISTAT 253
                CLEAR 7         ; restauramos modo de LISTAT y LISTOBJ
                DONE

M       _       CLW [0]         ; borra ventana activa
                DESC [1]

GRABA   RAM     RAMSAVE 0
                DONE

CARGA   RAM     RAMLOAD 0 255 255
                DESC [1]

CARGA   RAM     SYSMESS 8
                DONE

GRABA   _       SAVE
                DONE

CARGA   _       LOAD 255 255
                DESC [1]

RATON   _       ZERO 10
                MOUSE RATON_SI
                DONE
RATON   _       MOUSE RATON_NO
                DONE

FIN     _       QUIT
                EXIT 1
FIN     _       DONE

; ---------------------
;  Acci¢n COGER objeto
; ---------------------
COGER   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_coger
COGER   _       NEXTO 255       ; siguiente objeto si en bucle DOALL
COGER   _       AUTOG           ; coge objeto
                ISDOALL         ; si est† en bucle DOALL
                SKIP $doall_coger       ; repite con siguiente objeto
COGER   _       DONE            ; si no pudo coger, o cogi¢ y no en DOALL

; ---------------------
;  Acci¢n DEJAR objeto
; ---------------------
DEJAR   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_dejar
DEJAR   _       NEXTO 254       ; siguiente objeto si en bucle DOALL
DEJAR   _       AUTOD           ; deja objeto
                ISDOALL         ; si est† en bucle DOALL
                SKIP $doall_dejar       ; repite con siguiente objeto
DEJAR   _       DONE            ; si no pudo dejar, o dej¢ y no en DOALL

; ---------------------
;  Acci¢n PONER objeto
; ---------------------
PONER   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_poner
PONER   _       NEXTO 254       ; siguiente objeto si en bucle DOALL
PONER   _       AUTOW           ; pone objeto
                ISDOALL         ; si est† en bucle DOALL
                SKIP $doall_poner       ; repite con siguiente objeto
PONER   _       DONE            ; si no pudo poner, o puso y no en DOALL

; ----------------------
;  Acci¢n QUITAR objeto
; ----------------------
QUITAR  TODO    FIRSTO          ; inicio de bucle DOALL
$doall_quitar
QUITAR  _       NEXTO 253       ; siguiente objeto si en bucle DOALL
QUITAR  _       AUTOR           ; quita objeto
                ISDOALL         ; si est† en bucle DOALL
                SKIP $doall_quitar      ; repite con siguiente objeto
QUITAR  _       DONE            ; si no pudo quitar, o quit¢ y no en DOALL

EX      _       PROCESS 7       ; examinar
                DONE

DECIR   _       PROCESS 8
                DONE

TOCAR   CAMPAN  AT 3            ; si toca la campanilla
                ZERO 21         ; y no la toc¢ antes
                MES 2 3
                MES 2 0
                INK PPLANO_DIALOG
                MESSAGE 2 4     ; el bedel le har† caso
                INK PPLANO_VTXT
                SET 21
                DONE
TOCAR   CAMPAN  AT 3
                MES 2 3
                MES 2 0
                INK PPLANO_DIALOG
                MESSAGE 2 5
                INK PPLANO_VTXT
                DONE

; ----------------------------------------------------------------------------
_       _       SYSMESS 8
                DONE            ; hay m†s en l°nea input
; ----------------------------------------------------------------------------
\END
\PRO 3
; se ejecuta despuÇs de respuesta
\END
\PRO 4
; inicializaciones (def. ventanas,...)

_       _       NOTZERO 3       ; sale si no hay que hacer inicializaciones
                DONE
_       _       SET 3           ; indica que ya se han hecho inicializaciones

_       _       ABILITY 10

_       _       MODE MODO_256   ; inicializa modo de pantalla
                CHARSET 1       ; carga juego de caracteres

; definici¢n de ventanas
_       _       DEFWIN 0 0 255 0 0 255 255                      ; pantalla
                DEFWIN 1 FONDO_VTXT PPLANO_VTXT 13 0 40 12      ; texto
                WBORDER 1 WB_SENC_DOBLE
                DEFWIN 2 FONDO_VGRF PPLANO_VGRF 0 0 22 11       ; gr†ficos
                DEFWIN 3 FONDO_VBRUJ PPLANO_VBRUJ0 0 25 15 11   ; br£jula
                WINDOW 0

_       _       PLAYFLI 0 FLI_1
                COLOR 15
                PRINTAT 24 18
                MES 0 254
                PAUSE 0

_       _       MOUSE RATON_SI

_       _       GRAPHIC 0 1 GRF_MEM	; gr†fico de fondo

_       _       WINDOW 1
                CLW 1

_       _       DESC [1]        ; describe localidad actual (seguramente la 0)
\END
\PRO 5
; responde a teclas de funci¢n y acciones del rat¢n

_       _       EQ 12 TECLA_F10
                DEBUG DEBUG_SI          ; si pulsa F10 activa paso a paso
                DONE

$espera
_       _       EQ 12 BTN_RATON         ; si ha pulsado bot¢n del rat¢n
                MOUSEBT BTN_BOTON       ; espera a que lo suelte
                SKIP $espera

_       _       EQ 12 BTN_RATON
                ;print 18
                ;printc 45
                ;print 19
                MOUSEXY 0 0 10 21       ; si pulsa en ventana de gr†fico
                NEWLINE
                DESC [1]

_       _       EQ 12 BTN_RATON
                MOUSEXY 11 0 12 3       ; bot¢n 'Inv.'
                MESSAGE 0 3
                SET 7
                SYSMESS 14
                LISTAT 254
                SYSMESS 15
                LISTAT 253
                CLEAR 7
                DONE

_       _       EQ 12 BTN_RATON
                MOUSEXY 11 4 12 10      ; bot¢n 'Cargar'
                MESSAGE 0 4
                LOAD 255 255
                DESC [1]

_       _       EQ 12 BTN_RATON
                MOUSEXY 11 11 12 16     ; bot¢n 'Grabar'
                MESSAGE 0 5
                SAVE
                DONE

_       _       EQ 12 BTN_RATON
                MOUSEXY 11 17 13 21     ; bot¢n 'Salir'
                MESSAGE 0 6
                QUIT
                EXIT 1
                DONE

_       _       EQ 12 BTN_RATON
                MOUSEXY 0 30 1 30       ; 'NORTE'
                MESSAGE 0 7
                LET 3 1

_       _       EQ 12 BTN_RATON
                MOUSEXY 9 30 10 30      ; 'SUR'
                MESSAGE 0 8
                LET 3 2

_       _       EQ 12 BTN_RATON
                MOUSEXY 5 34 5 35       ; 'ESTE'
                MESSAGE 0 9
                LET 3 3

_       _       EQ 12 BTN_RATON
                MOUSEXY 5 25 5 26       ; 'OESTE'
                MESSAGE 0 10
                LET 3 4

_       _       EQ 12 BTN_RATON
                MOUSEXY 1 33 2 34       ; 'NORESTE'
                MESSAGE 0 11
                LET 3 5

_       _       EQ 12 BTN_RATON
                MOUSEXY 1 26 2 27       ; 'NOROESTE'
                MESSAGE 0 12
                LET 3 6

_       _       EQ 12 BTN_RATON
                MOUSEXY 8 33 9 34       ; 'SURESTE'
                MESSAGE 0 13
                LET 3 7

_       _       EQ 12 BTN_RATON
                MOUSEXY 8 26 9 27       ; 'SUROESTE'
                MESSAGE 0 14
                LET 3 8

_       _       ISMOV
                MOVE 1
                CLW [0]
                DESC [1]

_       _       ISMOV
                SYSMESS 21
                DONE
\END
\PRO 6
; impresi¢n de salidas de localidad actual
; Variables usadas: 254, 255

_       _       LET 254 [0]     ; guarda ventana actual
                WINDOW 3

_       _       SYNONYM NORTE NORTE
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 0 5
                PRINTC 226
                PRINTAT 1 5
                PRINTC 227

_       _       SYNONYM SUR SUR
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 9 5
                PRINTC 230
                PRINTAT 10 5
                PRINTC 231

_       _       SYNONYM ESTE ESTE
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 5 9
                PRINTC 228
                PRINTAT 5 10
                PRINTC 229

_       _       SYNONYM OESTE OESTE
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 5 0
                PRINTC 224
                PRINTAT 5 1
                PRINTC 225

_       _       SYNONYM NO NO
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 1 1
                PRINTC 232
                PRINTC 233
                PRINTAT 2 1
                PRINTC 234
                PRINTC 235

_       _       SYNONYM NE NE
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 1 8
                PRINTC 236
                PRINTC 237
                PRINTAT 2 8
                PRINTC 238
                PRINTC 239

_       _       SYNONYM SO SO
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 8 1
                PRINTC 240
                PRINTC 241
                PRINTAT 9 1
                PRINTC 242
                PRINTC 243

_       _       SYNONYM SE SE
                INK PPLANO_VBRUJ0
                LET 255 [1]
                MOVE 255
                INK PPLANO_VBRUJ1

_       _       PRINTAT 8 8
                PRINTC 244
                PRINTC 245
                PRINTAT 9 8
                PRINTC 246
                PRINTC 247

_       _       WINDOW  [254]           ; recupera ventana
\END
\PRO 7
; examinar objetos

_       PERIOD  PRESENT 0
                MOUSE RATON_NO
                MODE MODO_16
                DEFWIN 9 0 15 0 0 255 255
                WINDOW 9
                GRAPHIC 0 2 GRF_MEM     ; p†gina de peri¢dico
                DEFWIN 9 15 0 4 2 37 255        ; 1¶ columna
                MESSAGE 3 0
                MESSAGE 3 1
                DEFWIN 9 15 0 4 41 37 255       ; 2¶ columna
                MESSAGE 3 2
                MESSAGE 3 3
                MOUSE RATON_SI
                PAUSE 0
                PROCESS 11
                MODE MODO_256   ; restaura pantalla
                WINDOW 0
                GRAPHIC 0 1 GRF_MEM
                WINDOW 1
                CLW 1
                DESC [1]
                DONE

_       BEDEL   AT 3
                MESSAGE 0 0
                DONE

_       MESA    AT 3
                MESSAGE 0 2
                DONE

_       PUERTA  AT 4
                ZERO 22
                MESSAGE 0 15
                DONE

_       PUERTA  AT 4
                MESSAGE 0 16
                DONE

_       _       MESSAGE 1 0
                DONE
\END
\PRO 8
; di†logo con PSIs

_       _       AT 3
                EQ 3 PAL_NO
                SYNONYM DECIR BEDEL

_       _       AT 5
                EQ 3 PAL_NO
                SYNONYM DECIR MARKUS

_       BEDEL   AT 3
                PROCESS 9
                DONE

_       MARKUS  AT 5
                PROCESS 12
                DONE

_       _       MESSAGE 2 1
                NEWTEXT         ; deshecha el resto de la entrada
\END
\PRO 9
; di†logo con bedel

_       _       ZERO 21         ; si no ha tocado la campanilla
                MESSAGE 0 1     ; el bedel no hace ni caso
                NEWTEXT
                DONE

_       _       PARSE

_       _       MES 2 3
                MES 2 0
                INK PPLANO_DIALOG

_       HOLA    MESSAGE 2 6
                SKIP $fin

_       WILLIA  ZERO 22
                MESSAGE 2 7
                SET 22
                SKIP $fin

_       WILLIA  MESSAGE 2 8
                SKIP $fin

_       _       MESSAGE 2 2

$fin
_       _       INK PPLANO_VTXT
\END
\PRO 10
; dibuja gr†fico de localidad actual

_       _       WINDOW 2
                CLW 2
                GRAPHIC 1 [1] GRF_MEM

_       _       WINDOW 1
\END
\PRO 11
; fundido en negro para p†gina de peri¢dico

_       _       GETRGB 15 253 254 255   ; color 15=fondo de p†gina

$fundido
_       _       DEC 253
                DEC 254
                DEC 255
                REMAPC 15 [253] [254] [255]

_       _       EQ 253 0
                EQ 254 0
                EQ 255 0
                DONE

_       _       SKIP $fundido
\END
\PRO 12
; di†logo con Markus

_       _       PARSE

_       _       MES 2 9
                MES 2 0
                INK PPLANO_DIALOG

_       HOLA    MESSAGE 2 10
                SKIP $fin

_       SI      EQ 21 1         ; ®le ocurre algo a William?
                INC 21
                MESSAGE 2 12    ; datos de William
                SKIP $fin
_       NO      EQ 21 1         ; ®le ocurre algo a William?
                ADD 21 2
                MESSAGE 2 14    ; alivio
                SKIP $fin
_       SI      EQ 21 2         ; ®est† en peligro William?
                INC 21
                MESSAGE 2 17
                SKIP $fin
_       NO      EQ 21 2         ; ®est† en peligro William?
                INC 21
                MESSAGE 2 16
                SKIP $fin

_       WILLIA  EQ 21 0
                INC 21
                MESSAGE 2 11    ; sorpresa
                SKIP $fin
_       WILLIA  EQ 21 1         ; perspicacia
                MESSAGE 2 15
                SKIP $fin
_       WILLIA  MESSAGE 2 13    ; no sabe m†s
                SKIP $fin

_       _       MESSAGE 2 2

$fin
_       _       INK PPLANO_VTXT
\END
