; -----------------------------------------------
;   Aventura de demostraci¢n del sistema SINTAC
;             (c)1995 JSJ Soft Ltd.
; -----------------------------------------------

; ----------------------------------------------------------------------------
; VARIABLES:
;       46 -> usada como auxiliar en PRO 1
;       47 -> usada como auxiliar en varios procesos
;       48 y 49 -> contador de turnos parte alta-parte baja (0 a 65535)
;       50 -> usada por PRO 5 para impresi¢n de salidas
;       51 a 57 -> -----
;       58 -> n§ de monedas
;       59 -> usada por Proceso 10 para precio de las cosas
; ----------------------------------------------------------------------------
; BANDERAS:
;       3 -> usada por proceso 4 para ejecutar inicializaciones una vez s¢lo
;       50 -> usada para indicar si hubo alguna conexi¢n en PRO 5
;       51 -> linterna vac¡a (=0) o llena (=1)
;       52 -> botella de aceite vac¡a (=0) o llena (=1)
;       53 -> =1 si escuch¢ conversaci¢n en la taberna
;       54 -> =1 si salud¢ a los aventureros de la taberna
;       55 -> =1 si vi¢ el pulso entre Zora y el gigante
;       56 -> =1 si vi¢ a Mohamed
;       57 -> =1 si vi¢ al monje y al b rbaro
;       58 -> =1 si vi¢ a Zora
;       59 -> =1 si hech¢ el pulso
;       60 -> =1 si regal¢ una perla a Zora
;       61 -> =1 si habl¢ con Zora sobre la cueva de la cosa
;       62 -> =1 si pag¢ al Guardia
;       63 -> si =1 se salta PARSE en Proceso 2 (se activa cuando se pulsa
;             una tecla de funci¢n asignada a una acci¢n)
; ----------------------------------------------------------------------------

; definici¢n de constantes
\\V_MOV         14      ; m ximo n§ para verbos de movimiento
\\N_CONV        20      ; m ximo n§ para nombres convertibles
\\N_PROP        50      ; m ximo n§ para nombres propios

\\TECLA_F1      255     ; c¢digo INPUT de tecla F1
\\TECLA_F2      254     ; c¢digo INPUT de tecla F2
\\TECLA_F3      253     ; c¢digo INPUT de tecla F3
\\TECLA_F4      252     ; c¢digo INPUT de tecla F4
\\TECLA_F5      251     ; c¢digo INPUT de tecla F5
\\TECLA_F6      250     ; c¢digo INPUT de tecla F6
\\TECLA_F7      249     ; c¢digo INPUT de tecla F7
\\TECLA_F8      248     ; c¢digo INPUT de tecla F8
\\TECLA_F9      247     ; c¢digo INPUT de tecla F9
\\TECLA_F10     246     ; c¢digo INPUT de tecla F10
\MSY
@0 Has cogido _.|@
@1 Aqu¡ no est  eso.|@
@2 No puedes coger _. Llevas demasiadas cosas.|@
@3 Ya tienes eso.|@
@4 Dejas _.|@
@5 No tienes eso.|@
@6 |> @     ; Prompt
@7 Û@       ; Cursor
@8 No puedes hacer eso.|@
@9 |Tambi‚n puedes ver: @
@10 nada.|@
@11 , @
@12  y @
@13 .|@
@14 Tienes: @
@15 Llevas puesto: @
@16 Ya llevas puesto _.|@
@17 No puedes ponerte _.|@
@18 Te pones _.|@
@19 No llevas puesto eso.|@
@20 Te quitas _.|@
@21 No puedes ir en esa direcci¢n.|@
@22 --- Pulsa una tecla ---@
@23 Est  oscuro. No puedes ver nada.@
@24 ¨Est s seguro? @
@25 SN@
@26 Nombre del fichero: @
@27 Fichero ya existe. ¨Quieres continuar? @
@28 Error de apertura de fichero.|@
@29 Error de entrada/salida en fichero.|@
@30 Fichero no v lido.|@
@31 ¨Lo intentas de nuevo? @

@36 Turnos:       @
@37 |Puedes ver a@
; Ahora van los mensajes que ser n impresos como cabecera en cada localidad
@40 PLAZA DE PAUV@
@41 TABERNA@
@42 PUERTA DE PAUV@
@43 LLANURA@
@44 CUEVA@
@50  Mohamed@
@51  el Tabernero@
@52  el Monje@
@53  el B rbaro@
@54  Zora@
@55  el Guardia@
@56 |Teclas de funci¢n:||F1 - MIRAR|F2 - INVENTARIO|F3 - GRABAR|F4 - CARGAR|
F5 - FIN@
\END
\VOC
; ------------------------
;  Palabras de movimiento
; ------------------------
N               1       nombre
NORTE           1       nombre
S               2       nombre
SUR             2       nombre
E               3       nombre
ESTE            3       nombre
O               4       nombre
OESTE           4       nombre
NE              5       nombre
NORESTe         5       nombre
NO              6       nombre
NOROESte        6       nombre
SE              7       nombre
SURESTe         7       nombre
SO              8       nombre
SUROESte        8       nombre
ARRIBA          9       nombre
SUBIR           9       verbo
SUBE            9       verbo
SUBO            9       verbo
BAJAR           10      verbo
BAJA            10      verbo
BAJO            10      verbo
ABAJO           10      nombre
ENTRAR          11      verbo
ENTRA           11      verbo
ENTRO           11      verbo
SALIR           12      verbo
SALGO           12      verbo
SAL             12      verbo
; --------
;  Verbos
; --------
; NOTA: si un verbo tiene menos de 6 letras debes a¤adir las correspon-
; dientes para el verbo con terminaci¢n. Ej.: para DEJA tambi‚n hay que
; a¤adir en el vocabulario DEJALO, DEJALA, DEJALE.
COGER           20      verbo   ; Coger
COGERLo         20      verbo
COGE            20      verbo
COGELO          20      verbo
COGELA          20      verbo
COGELE          20      verbo
COJO            20      verbo

DEJAR           21      verbo   ; Dejar
DEJARLo         21      verbo
DEJA            21      verbo
DEJALO          21      verbo
DEJALA          21      verbo
DEJALE          21      verbo
DEJO            21      verbo

PONER           22      verbo   ; Poner
PONERLo         22      verbo
PONTE           22      verbo
PONTELo         22      verbo
PONGO           22      verbo
PON             22      verbo
PONLO           22      verbo
PONLA           22      verbo
PONLE           22      verbo
PONSELo         22      verbo
PONME           22      verbo
PONMELo         22      verbo

QUITAR          23      verbo   ; Quitar (prendas)
QUITA           23      verbo
QUITALo         23      verbo
QUITO           23      verbo

M               24      verbo   ; Mirar, describir
MIRAR           24      verbo
MIRARLo         24      verbo
MIRA            24      verbo
MIRALO          24      verbo
MIRALA          24      verbo
MIRALE          24      verbo
MIRO            24      verbo
DESCRIbir       24      verbo

FIN             25      verbo   ; Fin
ACABAR          25      verbo
ACABA           25      verbo
ACABO           25      verbo
TERMINar        25      verbo

EX              26      verbo   ; Examinar
EXAMINar        26      verbo
LEER            26      verbo   ; Leer
LEERLO          26      verbo
LEERLA          26      verbo
LEERLE          26      verbo
LEO             26      verbo
LEE             26      verbo
LEELO           26      verbo
LEELA           26      verbo
LEELE           26      verbo

SAVE            27      verbo   ; Grabar
GRABAR          27      verbo
GRABA           27      verbo
GRABO           27      verbo

LOAD            28      verbo   ; Cargar
CARGAR          28      verbo
CARGA           28      verbo
CARGO           28      verbo

DECIR           29      verbo   ; Decir, hablar, preguntar
DECIRLe         29      verbo
DIGO            29      verbo
DI              29      verbo
DILE            29      verbo
HABLAR          29      verbo
HABLA           29      verbo
PREGUNtar       29      verbo

ABRIR           30      verbo   ; Abrir
ABRIRLo         30      verbo
ABRO            30      verbo
ABRE            30      verbo
ABRELO          30      verbo
ABRELA          30      verbo
ABRELE          30      verbo

CERRAR          31      verbo   ; Cerrar
CIERRO          31      verbo
CIERRA          31      verbo

LUCHO           32      verbo   ; Luchar
LUCHAR          32      verbo
LUCHA           32      verbo
MATAR           32      verbo   ; Matar
MATARLe         32      verbo
MATA            32      verbo
MATALE          32      verbo
MATO            32      verbo
GOLPEAr         32      verbo   ; Golpear
ATACAR          32      verbo   ; Atacar
ATACA           32      verbo
ATACALe         32      verbo
ATACO           32      verbo
PEGAR           32      verbo
PEGO            32      verbo
PEGA            32      verbo
PEGALE          32      verbo

VALE            33      verbo   ; para preguntar cu nto cuestan las cosas
CUESTA          33      verbo

COMPRAr         34      verbo   ; sirve para COMPRAR, COMPRA, COMPRALO,...

ESCUCHar        35      verbo   ; para ESCUCHAR, ESCUCHA, ESCUCHALO
OIR             35      verbo
OYE             35      verbo
OIGO            35      verbo

ACEPTAr         36      verbo   ; para aceptar el reto del pulso con Zora
ACEPTO          36      verbo

PEDIR           37      verbo
PEDIRLo         37      verbo
PIDE            37      verbo
PIDELE          37      verbo
PIDESElo        37      verbo
PIDO            37      verbo

BEBER           38      verbo
BEBERLo         38      verbo
BEBE            38      verbo
BEBO            38      verbo
BEBELA          38      verbo
BEBELO          38      verbo
BEBETElo        38      verbo
TOMAR           38      verbo
TOMA            38      verbo
TOMO            38      verbo
TOMALA          38      verbo
TOMALO          38      verbo
TOMALE          38      verbo

DAR             39      verbo
DARLE           39      verbo
DARSELa         39      verbo
DA              39      verbo
DALE            39      verbo
DASELA          39      verbo
DASELO          39      verbo
DOY             39      verbo

PAGAR           40      verbo
PAGARLe         40      verbo
PAGA            40      verbo
PAGALE          40      verbo
PAGO            40      verbo

LLENAR          41      verbo
LLENA           41      verbo
LLENALo         41      verbo
LLENO           41      verbo

; ---------
;  Nombres
; ---------

; Convertibles (pueden usarse como verbos)
I               15      verbo   ; Inventario
INVENtario      15      verbo

; Propios (no les afectan las ternimaciones LO, LA, LE)
TODO            20      nombre
MOHAMEd         21      nombre
MONJE           22      nombre
BARBARo         23      nombre
ZORA            24      nombre
YO              25      nombre  ; por si quiere DECIR "YO" para aceptar pulso
TABERNero       26      nombre

; Otros nombres
RAM             50      nombre  ; para SAVE RAM y LOAD RAM
LINTERna        51      nombre
ESPADA          52      nombre
TENDERetes      53      nombre
HOMBREs         54      nombre
AVENTUreros     54      nombre
GRUPO           54      nombre
MUJER           55      nombre
MUJEREs         55      nombre
CARROS          56      nombre
JINETEs         57      nombre
GUARDIas        58      nombre
MONEDEro        59      nombre
MONEDA          59      nombre
DINERO          59      nombre
BOTELLa         60      nombre
ACEITE          60      nombre
CUERDA          61      nombre
PERFUMe         62      nombre
FRASCO          62      nombre
PERLA           63      nombre
PERLAS          63      nombre
BISUTEr¡a       64      nombre
BRAZALete       65      nombre
COLLAR          66      nombre
ANILLO          67      nombre
PRECIO          68      nombre
HOLA            69      nombre
CONVERsaci¢n    70      nombre
CHARLA          70      nombre
CUEVA           71      nombre
CAVERNa         71      nombre
COSA            72      nombre
PULSO           73      nombre
BEBIDA          74      nombre
CERVEZa         74      nombre
JARRA           74      nombre
SI              75      nombre
INSCRIpci¢n     76      nombre

; -----------
;  Adjetivos
; -----------
VIEJA           1       adjetivo
NEGRA           2       adjetivo
BLANCA          3       adjetivo

; --------------
;  Conjunciones
; --------------
Y               1       conjunci¢n
LUEGO           1       conjunci¢n
\END
\MSG 0
; mensajes generales
@0 ---Salidas---@
@1  NORTE@      ; textos para impresi¢n de salidas
@2  SUR@
@3  ESTE@
@4  OESTE@
@5  NINGUNA@
@6     @        ; mensaje para centrar salidas en ventana
@7 No ves nada especial.@       ; examinar objeto sin texto de examinar
@8 Es una vieja linterna de aceite.@
@9  Est  vac¡a.@
@10  Est  llena.@
@11 Llevas @
@12  moneda@
@13 Te llama la atenci¢n un tenderete atendido por un hombre gordo, que
 anuncia animosamente los productos expuestos: botellas llenas de ex¢ticos
 aceites, cuerdas, perfumes, perlas y objetos de bisuter¡a; de esos que
 vuelven locas a algunas mujeres.@
@14 Es un hombre gordo, su cara luce una expl‚ndida papada cubierta en
 parte por una barba bien afeitada. A voz en grito anuncia sus mercanc¡as.@
@15 Una peque¤a botella de aceite.@
@16 Cuerda fabricada en Laya-Hima, la ciudad de las c£pulas.@
@17 Perfume tra¡do del ex¢tico pa¡s de Kazdham.@
@18 Hay perlas negras de Knar y perlas blancas de las costas de
 Kuali-Lumpur.@
@19 Una hermosa perla negra de Knar.@
@20 Una reluciente perla blanca de Kuali-Lumpur.@
@21 Entre los objetos de bisuter¡a destacan brazaletes, collares y anillos.@
@22 Un brazalete finamente decorado, pero de poco valor.@
@23 Un collar con un medall¢n en el extremo que parece de poco valor.@
@24 Una gema que reluce mucho (pero es falsa) engastada en un aro de un
 metal poco valioso.@
@25 Botellas que contienen ex¢ticos aceites.@
@26 Una mellada espada con empu¤adura labrada de figuras geom‚tricas y
 una deslucida gema en el pu¤o.@
@27 Mohamed Al¡: "@
@28 Hola."@
@29 Toddo muy barato. Compdra, compdra..."@
@30 Aceidte cuedsta s¢lo 1 monedda, cuerdda 5 moneddas, ssuave perfume
 por 10 monedas, perlas valiossa padra tu mugjer 100 moneddas. Tambi‚n tener
 yo bdrazaletes por 3 moneddas, collares preciosos por 7 monedas y
 anilloss, mira, mira, por 4 monedas. T£, compdra, compdra..."@
@31 T£ no tenerr dinero. Yo no venderr si t£ no dinero."@
@32 T£ elegir bien. Toma.". Mohamed te entrega _.@
@33 ­Oh!, sentir mucsho. No me quedda dde eso."@
@34 Hay un nutrido grupo de aventureros que discuten mientras apuran
 las £ltimas gotas de sus jarras de cervezas.@
@35 "­Yo lo he visto!.", dice un hombre con h bitos de monje, "La £ltima noche
 cuando tuve que acampar cerca de la cueva. Mi mula sali¢ huyendo cuando
 aquella cosa rugi¢ desde el interior. Yo s¢lo vi sus brillantes ojos
 amarillos reflejados contra la oscuridad, pero fue tal el miedo que se
 apoder¢ de m¡ que sal¡ huyendo."|"­Ja!, t£ saldr¡as huyendo ante cualquier
 cosa.", replica un joven b rbaro con una mueca burlona en su rostro,
 "No creo que tal cosa exista. Todo son fantas¡as de mentes como la tuya.
 Quiz  s¢lo viste el reflejo de tu propio miedo en la oscuridad de esa cueva.
 Todos sabemos que la oscuridad te causa terror, ¨no es as¡?."|El resto
 de los aventureros r¡en del £ltimo comentario de su amigo.@
@36 El grupo de aventureros charla sobre cosas intrascendentes.@
@37 Monje: "@
@38 B rbaro: "@
@39 ¨Qu‚ quieres extranjero?, que me interrumpes con tan malos modales."@
@40 Te puedo asegurar que lo que v¡ no fue causa de mi miedo a la oscuridad.
 Esa cosa existe, y si alguien no hace nada para destruirla seguir  causando
 terror en toda la zona."@
@41 ¨De verdad crees esas historias para cr¡os?.", luego comienza a reirse
 a pleno pulm¢n.@
@42 Hola extranjero, ¨qu‚ se te ofrece?."@
@43 ¨Me hablas a m¡?."@
@44 Escuchas varias conversaciones mantenidas animosamente por las gentes
 de la taberna. Sobre todas te llama la atenci¢n la mantenida por un grupo
 de aventureros.@
@45 Escuchas atentamente y oyes el sonido de tu respiraci¢n.@
@46 Todas te parecen muy hermosas, especialmente una aventurera rubia que va
 acorazada de pies a cabeza y hecha un pulso con un gigante. La gente a su
 alrededor la aclama. "­Venga Zora!, t£ puedes con ‚l..." grita un hombre.
 El gigante parece tener dificultades, su rostro est  enrojecido y gru¤e
 por el esfuerzo. La mujer en cambio parece tan fresca como una rosa.
 Al final, el brazo del gigante cae sobre la mesa haci‚ndola a¤icos. Zora
 se levanta y grita: "­Bueno!, veamos qui‚n es el siguiente.". Un hombre con
 cara de rata entrega una bolsa de monedas a Zora la cual, sin abrirla, se la
 guarda r pidamente. El gigante sale de la taberna furioso, tanto que no
 se da cuenta de que atraviesa la pared trasera. El tabernero se lleva las
 manos a la cabeza.@
@47 Las mujeres que frecuentan esta taberna son todas recias aventureras o
 mercenarias contratadas por caravanas de mercaderes.@
@48 Parece un hombre insignificante embutido en sus ra¡dos h bitos, pero
 algo te dice que bajo esta apariencia se esconde una gran fuerza interna.@
@49 Es el t¡pico b rbaro de las llanuras. Su recia musculatura y su aspecto
 salvaje asombran a las mujeres y atemorizan a la mayor¡a de los hombres.@
@50 La armadura de placas que lleva no impide adivinar un cuerpo voluptuoso
 y c lido bajo el fr¡o acero. Su mirada es casi tan fr¡a como la espada que
 lleva colgando en la cintura.@
@51 Nadie te hace caso.@
@52 Tabernero: "@
@53 Yo no s‚ nada, ni he o¡do nada. As¡ que pide tu bebida o l rgate."@
@54 ­Oooh!, ssi, yo haber oiddo muchoss drumores de la cuevva. Pocos d¡ass
 que una cadravana despadrecerr misteriossamente. Pero t£ no miedo.
 ¨O si?", pregunta sonriente.@
@55 Si quieres beber, consigue dinero."@
@56 ­Aqu¡ tienes!, extranjero.", dice bruscamente.@
@57 Glu, glu, glu... ­Muy refrescante!.@
@58 Una jarra llena de espumosa cerveza.@
@59 Un tipo hosco, que atiende a los clientes y recoge las monedas que
 estos le entregan.@
@60 Primero deber¡as beberte la otra, ¨no te parece?."@
@61 Zora: "@
@62 ­Vaya!, un gallito de pelea. Veamos de lo que eres capaz. Apostaremos
 todo tu dinero contra el mio.". Zora se sienta y t£ enfrente suyo. Apoyais
 los codos sobre la mesa y entrecruz is vuestras manos. Sientes un fuerte
 apret¢n y como se tensan los m£sculos del brazo de Zora. Parece que lo vas
 a tener dificil...@
@63 Yo no pierdo el tiempo con vagabundos y mendigos. Cuando muestres tu oro
 veremos si se puede arreglar algo."@
@64 |Tus m£sculos se tensan por el esfuerzo pero el brazo de Zora no se mueve
 ni un mil¡metro...|@
@65 |Zora aprieta los dientes...|@
@66 |Tu brazo baja lentamente y Zora sonr¡e...|@
@67 |El brazo de Zora cede por momentos...|@
@68 |Con un £ltimo esfuerzo Zora gru¤e, sus m£sculos se abultan y sientes
 una oleada de fuerza irresistible. Cuando te das cuenta todos te miran. Tu
 brazo descansa sobre la mesa y Zora te lanza una mirada burlona. Zora
 extiende su mano en la cual t£ depositas hasta la £ltima moneda.|@
@69 |Sonr¡es al ver que tu oponente no puede resistir tu empuje. El brazo de
 Zora cae sobre la mesa al tiempo que algunos de los presentes gritan y te
 palmean la espalda. Zora te entrega una bolsa de monedas.|@
@70 Hola extranjero. ¨Aceptar¡as hechar un pulso con una d‚bil e indefensa
 mujer?.", te dice con una sonrisa maliciosa en su rostro.@
@71 Lo siento encanto, pero ya es suficienmte por hoy.".@
@72 Ummm, ¨dec¡as algo encanto?.@
; los mensajes 73 y 74 servir n para describir si un objeto es muy valioso
; o no; deben servir tanto para masculino como para femenino
@73 Parece muy valios@
@74 Parece      poco valios@
@75 Quiz  sepa algo, pero... ­Oye encanto!, ¨sab¡as que me apasionan
 las perlas?.", dice con un tono insinuante.@
@76 La cueva donde habita esa cosa queda hacia el este. Es un viaje largo.
 Adem s tengo entendido que le atraen las cosas brillantes. Si vas all¡
 te deseo buena suerte encanto.".@
@77 ­Oooh!, ­qu‚ preciosidad!. Eres generoso extranjero. Ahora dime lo que
 quieres saber.".@
@78 ¨A qui‚n?.@
@79 Nadie parece querer una perla.@
@80 Guardia: "@
@81 ­Alto ah¡!, nadie sale de la ciudad sin pagar los impuestos."@
@82 Muy bien, puedes continuar tu camino."@
@83 ­Ja!, veo que est s sin blanca. ¨Acaso pretend¡as enga¤arme?."@
@84 Parece un tipo muy engre¡do enfundado en su recia armadura.@
@85 ¨C£al gudstarr m ss?."@
@86 Abandonas la ciudad y te encaminas en direcci¢n norte, hacia los p ramos
 donde decidir s tu verdadero camino...@
@87 Caminas durante horas pero el paisaje parece repetirse...@
@88 Despu‚s de duras horas de marcha llegas a las puertas de la ciudad de
 Pauv. Varios viajeros y caravanas esperan para entrar. Cuando te toca el
 turno los guardias te hacen unas preguntas rutinarias y te dejan pasar...@
@89 Varias horas de marcha en direcci¢n este te conducen a un paraje
 desolado d¢nde no crece ni una brizna de hierba. A lo lejos ves una peque¤a
 loma con el agujero de una cueva en su ladera. Te diriges hacia all¡ ya que
 ese parece ser tu destino...@
@90 Muchas gracias.".@
@91 Llenas la linterna de aceite.@
@92 ¨Con qu‚?@
@93 No te queda aceite.@
@94 ­Bueno! supongo que esperabas encontrar una criatura enorme y babeante
 esper ndote en la oscuridad de esta cueva... Pues estabas totalmente
 equivocado. Al fin y al cabo los rumores que has oido pueden haber sido
 falsos. ¨O no?. Lo cierto es que nunca sabr s si... ­Oh no!. ¨Qu‚ demonios
 es ese ruido?. ­Cielos!, ­no es posible!, NOOOO, AAARRGH...@
\END
\LOC
@0 Te encuentras en la plaza de la ciudad de Pauv, ciudad donde se reunen
 gentes de todo el continente. En esta plaza se llevan a cabo la mayor¡a de
 las transacciones comerciales. Puedes ver un mont¢n de tenderetes donde se
 venden mil y una cosas; desde verduras hasta esclavos. Siguiendo un
 callej¢n, al norte de aqu¡, queda la m s concurrida posada del continente.@
# NORTE 1
# ESTE  2

@1 La taberna se encuentra abarrotada de gentes de todo tipo. El ambiente
 est  cargado en exceso y se oyen las risas de los hombres y las mujeres
 que ahogan sus penas en unas cuantas jarras de alcohol.@
# SUR 0

@2 Los carros, jinetes y gentes a pie se agolpan frente a las puertas de
 entrada de la gran urbe. Los guardias, enfundados en relucientes armaduras
 de escamas, vigilan noche y d¡a estas puertas.@
# OESTE 0
# NORTE 3

@3 Ante t¡ se extiende una gran llanura. Los altos picos de las monta¤as
 casi ara¤an las nubes all  en el norte. Hacia el oeste divisas las lindes de
 un bosque y hacia el este la llanura se extiende m s all  del horizonte.@
# NORTE 3       ; en principio colocamos las salidas de esta localidad
# SUR   3       ; apuntando hacia s¡ misma, para que aparezcan listadas en
# ESTE  3       ; pantalla; luego en \PRO 2 bifurcaremos a la localidad
# OESTE 3       ; correspondiente

@4 Un olor f‚tido invade esta cueva, una cueva que parece excavada por
 algo enorme...@
\END
\OBJ
; Banderas usuario ÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
; Tipo (Prenda, Luz) ÄÄÄÄÄÄÄÄÄÄÄ¿
; Loc. inicio ÄÄÄÄÄÄÄÄÄÄ¿       ³
;   252 - no creado     ³       ³
;   253 - puesto        ³       ³
;   254 - cogido        ³       ³ MASC./FEMEN. ÄÄÄ¿
; Adjetivo ÄÄÄÄÄÄÄ¿     ³       ³ OBJ.POCO VALIOSO³
; Nombre ¿        ³     ³       ³ OBJ.VALIOSO ÄÄ¿³³
;                                               ³³³
@0 LINTERNA     VIEJA   254     L               OOOOOOOOOOOOOOOO
una vieja linterna
@1 ESPADA       _       254                     XOXOOOOOOOOOOOOO
una espada
@2 MONEDero     _       254                     OOOOOOOOOOOOOOOO
un monedero
@3 BOTELLA      _       252                     OOOOOOOOOOOOOOOO
una botella de aceite
@4 CUERDA       _       252                     OOOOOOOOOOOOOOOO
un rollo de cuerda
@5 PERFUME      _       252                     OOOOOOOOOOOOOOOO
un frasco de perfume
@6 PERLA        NEGRA   252                     XOXOOOOOOOOOOOOO
una perla negra
@7 PERLA        BLANCA  252                     XOXOOOOOOOOOOOOO
una perla blanca
@8 BRAZALETE    _       252                     OXOOOOOOOOOOOOOO
un brazalete
@9 COLLAR       _       252                     OXOOOOOOOOOOOOOO
un collar
@10 ANILLO      _       252                     OXOOOOOOOOOOOOOO
un anillo
@11 CERVEZA     _       252                     OOOOOOOOOOOOOOOO
una jarra de cerveza
@12 INSCRIPCION _       4                       OOOOOOOOOOOOOOOO
una inscripci¢n en la pared
\END
\PRO 0
; se ejecuta tras cada inicializaci¢n y tras cada condacto DESC

_       _       CLEAR 63        ; borra bandera 63 por si est  activa

_       _       PROCESS 4       ; inicializaciones
_       _       NOTZERO 2       ; si hay que saltar acciones DESC
                                ; (ej, RESTART)
                SKIP $buc_princ
_       _       SET 2           ; aqu¡ llega si es inicio del juego o DESC
; Ejecuta diversas acciones tras una descripci¢n.
_       _       NORESP
_       _       LISTOBJ         ; lista objetos
                PROCESS 1       ; el Proceso 1 se ejecuta tras una descripci¢n
$buc_princ
_	_	TIMEOUT
		PROCESS 3
_       _       SYSMESS 6
                INPUT           ; pide frase de entrada
                PROCESS 20      ; procesa teclas de funci¢n
                SKIP $buc_princ ; si no tecle¢ nada vuelve a pedir
_       _       NEWLINE
$buc_sec
_       _       PROCESS 2       ; analiza, salta a sgte. entr. si no hay m s
                NORESP
                PROCESS 3       ; ejecuta despu‚s de respuesta
                ZERO 63         ; si no se activ¢ bandera 63
                SKIP $buc_sec   ; repite por si queda algo por analizar
_       _       CLEAR 63        ; borra bandera 63
                SKIP $buc_princ ; vuelve a pedir frase de entrada
\END
\PRO 1
; se ejecuta tras una descripci¢n
_       _       PROCESS 21
; presenta los PSIs que hay en la localidad actual y de los que el
; jugador tiene conocimiento
_       _       AT 0
                NOTZERO 56
                SYSMESS 37
                SYSMESS 50
_       _       AT 1
                SYSMESS 37
                SYSMESS 51
_       _       AT 1
                NOTZERO 57
                PRINTC 44
                SYSMESS 52
                PRINTC 44
                SYSMESS 53
_       _       AT 1
                NOTZERO 58
                PRINTC 44
                SYSMESS 54
_       _       AT 2
                SYSMESS 37
                SYSMESS 55

_       _       NEWLINE

_       _       LET 47 [0]      ; guarda n§ de ventana activa
                WINDOW 2
                CLW 2
                PROCESS 5       ; imprime las salidas

                WINDOW 5
                CLW 5
                SYSMESS 56      ; imprime teclas de funci¢n

                WINDOW 4
                CLW 4
                PRINTAT 0 4
                LET 46 [1]      ; n§ de localidad actual -> var. 46
                ADD 46 40       ; convierte en n§ de mensaje sistema
                SYSMESS [46]    ; imprime encabezado localidad
                PROCESS 11      ; incrementa contador de turnos
                PRINTAT 0 43    ; imprime Turnos:
                SYSMESS 36
                PRINTAT 0 51
                DPRINT 48

                WINDOW [47]     ; restaura ventana que estaba activa
\END
\PRO 2
; tabla de Respuestas

_       _       ZERO 63         ; si bandera 63 no es 1
                PARSE           ; analiza la frase
                NOTDONE         ; no hay m s

_       _       RESP

_       NORTE   AT 2            ; si quiere salir de la ciudad
                ZERO 62
                MES 0 80        ; tendr  que pagar antes al guardia
                MESSAGE 0 81
                DONE
_       NORTE   AT 2            ; si sale de la ciudad
                CLW [0]         ; borra ventana activa
                MESSAGE 0 86    ; imprime mensaje de salida de ciudad
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                GOTO 3          ; y le manda a la llanura
                DESC [1]
_       NORTE   AT 3            ; va hacia el norte en llanura...
                CLW [0]
                MESSAGE 0 87    ; ...y no llega a ning£n sitio
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                DESC [1]
_       SUR     AT 3            ; si va al sur regresa a la ciudad
                CLW [0]
                MESSAGE 0 88
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                GOTO 2
                DESC [1]
_       OESTE   AT 3            ; hacia el oeste no va a ning£n sitio
                CLW [0]
                MESSAGE 0 87
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                DESC [1]
_       ESTE    AT 3            ; y al este sin saber que la cueva
                ZERO 61         ; est  por all¡
                CLW [0]
                MESSAGE 0 87
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                DESC [1]
_       ESTE    AT 3            ; si sabe de la existencia de la cueva
                CLW [0]         ; le permite ir hacia all¡
                MESSAGE 0 89
                PAUSE 40
                NEWLINE
                ANYKEY
                CLW [0]
                GOTO 4
                DESC [1]

_       _       ISMOV           ; si es movimiento
                MOVE 1          ; intenta mover en esa direcci¢n
                CLW [0]         ; si pudo moverse, borra ventana activa
                DESC [1]        ; y describe localidad
_       _       ISMOV
                SYSMESS 21      ; si no pudo moverse
                DONE

I       _       SET 7
                SYSMESS 14
                LISTAT 254
                SYSMESS 15
                LISTAT 253
                CLEAR 7
                DONE

M       _       CLW [0]         ; borra ventana activa
                DESC [1]

GRABA   RAM     RAMSAVE 0
                DONE

CARGA   RAM     RAMLOAD 0 255 255
                DESC [1]

CARGA   RAM     SYSMESS 8
                DONE

GRABA   _       SAVE
                DONE

CARGA   _       LOAD 255 255
                DESC [1]

FIN     _       QUIT
                EXIT 1
FIN     _       DONE

; ---------------------
;  Acci¢n COGER objeto
; ---------------------
COGER   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_coger
COGER   _       NEXTO 255       ; siguiente objeto si en bucle DOALL
COGER   _       AUTOG           ; coge objeto
                ISDOALL         ; si est  en bucle DOALL
                SKIP $doall_coger       ; repite con siguiente objeto
COGER   _       DONE            ; si no pudo coger, o cogi¢ y no en DOALL

; ---------------------
;  Acci¢n DEJAR objeto
; ---------------------
DEJAR   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_dejar
DEJAR   _       NEXTO 254       ; siguiente objeto si en bucle DOALL
DEJAR   _       AUTOD           ; deja objeto
                ISDOALL         ; si est  en bucle DOALL
                SKIP $doall_dejar       ; repite con siguiente objeto
DEJAR   _       DONE            ; si no pudo dejar, o dej¢ y no en DOALL

; ---------------------
;  Acci¢n PONER objeto
; ---------------------
PONER   TODO    FIRSTO          ; inicio de bucle DOALL
$doall_poner
PONER   _       NEXTO 254       ; siguiente objeto si en bucle DOALL
PONER   _       AUTOW           ; pone objeto
                ISDOALL         ; si est  en bucle DOALL
                SKIP $doall_poner       ; repite con siguiente objeto
PONER   _       DONE            ; si no pudo poner, o puso y no en DOALL

; ----------------------
;  Acci¢n QUITAR objeto
; ----------------------
QUITAR  TODO    FIRSTO          ; inicio de bucle DOALL
$doall_quitar
QUITAR  _       NEXTO 253       ; siguiente objeto si en bucle DOALL
QUITAR  _       AUTOR           ; quita objeto
                ISDOALL         ; si est  en bucle DOALL
                SKIP $doall_quitar      ; repite con siguiente objeto
QUITAR  _       DONE            ; si no pudo quitar, o quit¢ y no en DOALL

; -----------------
;  Acci¢n EXAMINAR
; -----------------
EXAMINA _       PROCESS 7
                DONE

; -----------------
;  HABLAR con PSIs
; -----------------
DECIR   _       PROCESS 8
                DONE

; ---------------
;  COMPRAR cosas
; ---------------
COMPRAR _       PROCESS 10
                DONE

;  Escuchar conversaci¢n en la taberna
OIR     HOMBRES SYNONYM _ CONVERSACION
OIR     CONVERS AT 1
                ZERO 53
                SET 53
                SET 57          ; para que B rbaro y Monje aparezcan en lista
                MESSAGE 0 35
                DONE
OIR     CONVERS AT 1
                MESSAGE 0 36
                DONE
OIR     _       AT 1            ; si escucha en la taberna
                MESSAGE 0 44    ; oir  a los aventureros charlando
                DONE
OIR     _       MESSAGE 0 45    ; si escucha en otros sitios
                DONE            ; no oir  nada de nada...

; pedirle una bebida al tabernero
PEDIR   BEBIDA  AT 1
                MES 0 52
                PROCESS 15
                DONE

; beber la cerveza
BEBER   CERVEZA CARRIED 11
                MESSAGE 0 57
                DESTROY 11
                DONE

; aceptar el pulso con Zora
ACEPTAR _       SYNONYM _ PULSO
_       PULSO   AT 1            ; si est  en la taberna
                NOTZERO 55      ; vi¢ el pulso entre Zora y el gigante
                NOTZERO 59      ; y ya ha hecho el pulso
                MES 0 61
                MESSAGE 0 71    ; Zora no quiere otro pulso
                DONE
_       PULSO   AT 1            ; si est  en la taberna
                NOTZERO 55      ; vi¢ el pulso entre Zora y el gigante
                CARRIED 2       ; y lleva dinero
                NOTEQ 58 0
                MES 0 61
                MESSAGE 0 62    ; Zora acepta
                PROCESS 17      ; se hace el pulso
                SET 59          ; y lo indicamos
                DONE
_       PULSO   AT 1            ; si est  en la taberna
                NOTZERO 55      ; vi¢ el pulso entre Zora y el gigante
                MES 0 61        ; pero est  sin dinero
                MESSAGE 0 63    ; Zora le dice que vuelva cuando lo tenga
                DONE

; darle una perla a Zora
DAR     PERLA   NOUN2 ZORA      ; si le da la perla a Zora
                AT 1
                WHATO
                CARRIED [8]     ; ¨lleva una perla?
                MES 0 61
                MESSAGE 0 77
                SET 60          ; indica que le di¢ la perla
                DESTROY [8]
                DONE
DAR     PERLA   NOUN2 TABERNERO ; si le da la perla al Tabernero...
                AT 1
                WHATO
                CARRIED [8]
                MES 0 52
                MESSAGE 0 90
                DESTROY [8]
                DONE
DAR     PERLA   NOUN2 MONJE     ; ...o al Monje...
                AT 1
                NOTZERO 57
                WHATO
                CARRIED [8]
                MES 0 37
                MESSAGE 0 90
                DESTROY [8]
                DONE
DAR     PERLA   NOUN2 BARBARO   ; ...o al B rbaro, la pierde
                AT 1
                NOTZERO 57
                WHATO
                CARRIED [8]
                MES 0 38
                MESSAGE 0 90
                DESTROY [8]
                DONE
DAR     PERLA   AT 1
                WHATO
                CARRIED [8]     ; ¨lleva una perla?
                MESSAGE 0 78    ; ¨a qui‚n se la da?
                DONE
DAR     PERLA   WHATO
                CARRIED [8]     ; ¨lleva una perla?
                MESSAGE 0 79    ; nadie la quiere
                DONE

; pagar al guardia
DAR     MONEDA  AT 2
                SYNONYM PAGAR _
PAGAR   _       AT 2            ; si est  a las puertas de la ciudad
                CARRIED 2       ; y lleva dinero
                NOTEQ 58 0
                SET 62          ; indica que ya ha pagado
                DEC 58          ; y le quita una moneda
                MES 0 80
                MESSAGE 0 82
                DONE
PAGAR   _       AT 2            ; si no tiene dinero
                MES 0 80
                MESSAGE 0 83
                DONE

; llenar la linterna de aceite
LLENAR  LINTERN NOTCARR 0       ; si no lleva la linterna
                SYSMESS 5       ; le dice que no la tiene
                DONE
LLENAR  LINTERN NOTCARR 3       ; si no lleva la botella de aceite
                MESSAGE 0 92    ; ¨con qu‚ la va a llenar?
                DONE
LLENAR  LINTERN ZERO 52         ; si la lleva pero est  vac¡a...
                MESSAGE 0 93
                DONE
LLENAR  LINTERN MESSAGE 0 91    ; si lleva la linterna y la botella
                SET 51          ; de aceite llena, llena la linterna
                CLEAR 52        ; y vac¡a la botella
                DONE

; -----------------------------------------------------------
_       _       SYSMESS 8
                DONE            ; hay m s en l¡nea input
; -----------------------------------------------------------
\END
\PRO 3
; se ejecuta despu‚s de respuesta

_       _       LET 47 [0]      ; guarda n§ de ventana actual
                WINDOW 4
                PROCESS 11      ; incrementa contador de turnos
                PRINTAT 0 43    ; imprime los turnos que lleva
                SYSMESS 36
                PRINTAT 0 51
                DPRINT 48
                WINDOW [47]     ; vuelve a la ventana que estaba activa
\END
\PRO 4
; inicializaciones (def. ventanas,...)

_       _       NOTZERO 3       ; sale si no hay que hacer inicializaciones
                DONE
_       _       SET 3           ; indica que ya se han hecho inicializaciones

_       _       ABILITY 10      ; permite llevar hasta 10 objetos
                SEED 0          ; inicializa generador de n£meros aleatorios
                LET 58 10       ; monedas iniciales

_       _       MODE 0

_       _       DEFWIN 0 0 7 255 255 255 255    ; toda la pantalla
                DEFWIN 1 0 15 17 0 255 255      ; ventana principal
                WBORDER 1 4
                DEFWIN 2 0 15 1 36 19 16        ; ventana de salidas
                WBORDER 2 3
                DEFWIN 3 0 7 1 0 36 16          ; ventana de gr ficos
                DEFWIN 4 1 15 0 0 255 1         ; sub-ventana r¢tulos
                DEFWIN 5 0 15 1 55 25 16        ; ventana teclas funci¢n
                WBORDER 5 3

_       _       CHARSET 1

; PRESENTACION...
_       _       WINDOW 0
                GRAPHIC 0 0 1   ; intenta imprimir gr fico de presentaci¢n
                PAUSE 0
                CLS

_       _       SET 52          ; inicialmente la botella est  llena de aceite

_       _       CLW 1
                CLW 2
                CLW 3
                CLW 4
                CLW 5
                WINDOW 1

_       _       DESC [1]        ; describe localidad actual (seguramente la 0)
                NEWLINE
\END
\PRO 5
; imprime las salidas
; Utilizamos la variable 50 para contener el n£mero de localidad
; y la bandera 50 como indicador de si hubo alguna conexi¢n o no

_       _       PRINTAT 0 2
                MESSAGE 0 0
                NEWLINE
                CLEAR 50                ; borra indicador de salidas
_       _       SYNONYM NORTE NORTE     ; comprobamos hacia NORTE
                LET 50 [1]
                MOVE 50
                MES 0 6
                MESSAGE 0 1
                SET 50
_       _       SYNONYM SUR SUR         ; comprobamos hacia SUR
                LET 50 [1]
                MOVE 50
                MES 0 6
                MESSAGE 0 2
                SET 50
_       _       SYNONYM ESTE ESTE       ; comprobamos hacia ESTE
                LET 50 [1]
                MOVE 50
                MES 0 6
                MESSAGE 0 3
                SET 50
_       _       SYNONYM OESTE OESTE     ; comprobamos hacia OESTE
                LET 50 [1]
                MOVE 50
                MES 0 6
                MESSAGE 0 4
                SET 50
_       _       ZERO 50                 ; si no se encontr¢ ninguna salida
                MES 0 6
                MESSAGE 0 5
\END
\PRO 7
; examinar

_       _       WHATO
_       _       EQ 8 255        ; si no es un objeto v lido
                SKIP $descripci¢n
_       _       HASAT 0         ; objeto valioso
                MES 0 73
                PROCESS 18      ; imprime 'o.' o 'a.' dependiendo de g‚nero
_       _       HASAT 1         ; objeto poco valioso
                MES 0 74
                PROCESS 18      ; imprime 'o.' o 'a.' dependiendo de g‚nero
$descripci¢n
_       LINTER  PRESENT 0
                MES 0 8
                ZERO 51
                MESSAGE 0 9       ; linterna vac¡a
                DONE
_       LINTER  PRESENT 0
                MESSAGE 0 10    ; linterna llena
                DONE
_       ESPADA  PRESENT 1
                MESSAGE 0 26
                DONE
_       MONEDAS CARRIED 2       ; Si lleva monedero...
                MES 0 11        ; Llevas
                PRINT 58        ; imprime el n§ de monedas
                MES 0 12
_       MONEDAS CARRIED 2
                NOTEQ 58 1      ; si lleva 0 o m s de 1 monedas
                PRINTC 115      ; imprime 's'
_       MONEDAS CARRIED 2
                PRINTC 46       ; imprime '.'
                NEWLINE
                DONE
_       TENDER  AT 0            ; tenderete de la plaza de la ciudad
                MESSAGE 0 13
                SET 56          ; para que Mohamed aparezca en lista PSIs...
                DONE
_       HOMBRE  AT 0            ; si est  en loc. 0 supone Mohamed
                SYNONYM _ MOHAMED
_       MOHAMED AT 0
                MESSAGE 0 14
                SET 56          ; para que Mohamed aparezca en lista PSIs...
                DONE
_       BOTELLA PRESENT 3       ; botella de aceite
                MES 0 15
_       BOTELLA PRESENT 3       ; est  vac¡a
                ZERO 52
                MESSAGE 0 9
                DONE
_       BOTELLA PRESENT 3       ; est  llena
                MESSAGE 0 10
                DONE
_       BOTELLA AT 0            ; si no est  la botella pero est  en
                MESSAGE 0 25    ; localidad de tenderete
                DONE
_       CUERDA  PRESENT 4       ; si est  la cuerda
                MESSAGE 0 16
                DONE
_       CUERDA  AT 0            ; si no est  la cuerda pero est 
                MESSAGE 0 16    ; en localidad de tenderete
                DONE
_       PERFUME PRESENT 5       ; si est  el perfume
                MESSAGE 0 17
                DONE
_       PERFUME AT 0            ; si no est , pero est  en tenderete
                MESSAGE 0 17
                DONE
_       PERLA   ADJECT1 NEGRA   ; examina perla negra
                PRESENT 6
                MESSAGE 0 19
                DONE
_       PERLA   ADJECT1 BLANCA  ; examina perla blanca
                PRESENT 7
                MESSAGE 0 20
                DONE
_       PERLA   PRESENT 6       ; si no especific¢ el tipo de perla
                MESSAGE 0 19    ; probamos si est  la negra
                DONE
_       PERLA   PRESENT 7       ; si no la blanca
                MESSAGE 0 20
                DONE
_       PERLA   AT 0            ; si no, si est  en tenderete
                MESSAGE 0 18
                DONE
_       BISUTER AT 0            ; bisuter¡a del tenderete
                MESSAGE 0 21
                DONE
_       BRAZAL  PRESENT 8       ; brazalete
                MESSAGE 0 22
                DONE
_       COLLAR  PRESENT 9
                MESSAGE 0 23
                DONE
_       ANILLO  PRESENT 10
                MESSAGE 0 24
                DONE
_       JARRA   PRESENT 11
                MESSAGE 0 58
                DONE

_       HOMBRES AT 1            ; examinar hombres taberna
                MESSAGE 0 34
                DONE
_       MUJERES AT 1            ; si est  en la taberna
                ZERO 55         ; y no vi¢ el pulso entre Zora y el
                SET 55          ; gigante, se lo muestra ahora...
                SET 58          ; para que Zora aparezca en lista de PSIs...
                MESSAGE 0 46
                DONE
_       MUJERES AT 1            ; examinar mujeres de la taberna
                MESSAGE 0 47
                DONE
_       MONJE   AT 1
                NOTZERO 57      ; si sabe que hay un monje
                MESSAGE 0 48    ; descripci¢n del Monje
                DONE
_       BARBARO AT 1
                NOTZERO 57      ; si sabe que hay un b rbaro
                MESSAGE 0 49    ; descripci¢n del B rbaro
                DONE
_       ZORA    AT 1
                NOTZERO 58      ; si sabe que est  Zora
                MESSAGE 0 50    ; descripci¢n de Zora
                DONE
_       TABERN  AT 1
                MESSAGE 0 59
                DONE
_       GUARDIA AT 2
                MESSAGE 0 84
                DONE
_       INSCRIP AT 4
                CLS
                DEFWIN 5 2 10 8 19 40 14
                WBORDER 5 2
                CLW 5
                WINDOW 5
                MESSAGE 0 94
                NEWLINE
                END             ; FINAL de la aventura (?)

_       _       MESSAGE 0 7     ; no ves nada especial
\END
\PRO 8
; hablar con PSIs

_       _       EQ 3 255        ; si no introdujo nombre
                AT 0            ; y est  en localidad 0
                SYNONYM _ MOHAMED       ; supone que habla con Mohamed
_       _       EQ 3 255        ; si no introdujo nombre
                AT 1            ; y est  en localidad 1
                SYNONYM _ TABERNERO     ; supone que habla con Tabernero
_       _       EQ 3 255        ; si no introdujo nombre
                AT 2            ; y est  en localidad 2
                SYNONYM _ GUARDIA       ; supone que habla con Guardia
_       HOMBRE  AT 0    ; si est  en loc. 0 supone Mohamed
                SYNONYM _ MOHAMED
_       HOMBRE  AT 1    ; si est  en loc. 1 supone Tabernero
                SYNONYM _ TABERNERO
_       HOMBRE  AT 2    ; si est  en loc. 2 supone Guardia
                SYNONYM _ GUARDIA

_       MOHAMED AT 0
                NOTZERO 56      ; si vi¢ a Mohamed
                PROCESS 9
                DONE
_       TABERN  AT 1
                PROCESS 14
                DONE
_       BARBARO AT 1
                NOTZERO 57      ; si vi¢ al B rbaro y al Monje
                PROCESS 12
                DONE
_       MONJE   AT 1
                NOTZERO 57      ; si vi¢ al B rbaro y al Monje
                PROCESS 13
                DONE
_       ZORA    AT 1
                NOTZERO 58      ; si vi¢ a Zora
                PROCESS 16
                DONE
_       GUARDIA AT 2
                PROCESS 19
                DONE

_       _       MESSAGE 0 51
                NEWTEXT         ; deshecha resto de la l¡nea
                DONE
\END
\PRO 9
; hablar con Mohamed

_       _       PARSE   ; analiza frase para Mohamed
_       _       MES 0 27        ; introducci¢n a frase de Mohamed
_       HOLA    MESSAGE 0 28
                DONE
CUESTA  _       SYNONYM _ PRECIO
_       PRECIO  MESSAGE 0 30
                DONE
_       CUEVA   SYNONYM _ COSA
_       COSA    MESSAGE 0 54
                DONE
_       _       MESSAGE 0 29
\END
\PRO 10
; comprar cosas a Mohamed
; se usa la bandera 59 para almacenar temporalmente el precio
; de las cosas.

_       _       MES 0 27        ; introducci¢n a frase de Mohamed
                WHATO           ; n£mero de objeto referenciado -> var. 8
_       _       NOTCARR 2       ; si no lleva las monedas
                MESSAGE 0 31
                DONE
_       BOTELLA LET 59 1        ; botella cuesta 1 moneda
                SKIP $compra
_       CUERDA  LET 59 5        ; cuerda cuesta 5 monedas
                SKIP $compra
_       PERFUME LET 59 10       ; perfume cuesta 10 monedas
                SKIP $compra
_       PERLA   EQ 4 255        ; si no especifica qu‚ perla quiere
                MESSAGE 0 85
                DONE
_       PERLA   LET 59 100      ; las perlas cuestan 100 monedas
                SKIP $compra
_       BRAZAL  LET 59 3        ; el brazalete cuesta 3 monedas
                SKIP $compra
_       COLLAR  LET 59 7        ; el collar cuesta 7 monedas
                SKIP $compra
_       ANILLO  LET 59 4        ; el anillo cuesta 4 monedas
                SKIP $compra
_       _       MESSAGE 0 33    ; no tiene de eso
                DONE
$compra
_       _       ISAT [8] 252    ; objeto no vendido todav¡a
                LT 58 [59]      ; pero no las suficientes
                MESSAGE 0 31
                DONE
_       _       ISAT [8] 252    ; objeto no vendido todav¡a
                SUB 58 [59]     ; quita el precio del total de monedas
                MESSAGE 0 32    ; informa de la venta
                CREATE [8]      ; crea el objeto
                GET [8]         ; y lo coge (si puede llevar m s cosas)
                DONE
_       _       MESSAGE 0 33    ; no le queda de eso
                DONE
\END
\PRO 11
; incrementa el contador de turnos

_       _       INC 49          ; incrementa parte baja contador turnos
                EQ 49 0         ; si ha pasado de 255 a 0
                INC 48          ; incrementa parte alta
\END
\PRO 12
; hablar con B rbaro

_       _       PARSE
_       _       MES 0 38        ; introducci¢n al B rbaro
_       HOLA    MESSAGE 0 42    ; presentarse
                SET 54          ; indicador de que se ha presentado
                DONE
_       CUEVA   SYNONYM _ COSA
_       COSA    ZERO 54         ; si no se present¢
                MESSAGE 0 39    ; le indica 'amablemente' sus malos modales
                DONE
_       COSA    MESSAGE 0 41
                DONE
_       _       MESSAGE 0 43
\END
\PRO 13
; hablar con Monje

_       _       PARSE
_       _       MES 0 37        ; introducci¢n al Monje
_       HOLA    MESSAGE 0 42    ; presentarse
                SET 54          ; indicador de que se ha presentado
                DONE
_       CUEVA   SYNONYM _ COSA
_       COSA    ZERO 54         ; si no se present¢
                MESSAGE 0 39    ; le indica 'amablemente' sus malos modales
                DONE
_       COSA    MESSAGE 0 40
                DONE
_       _       MESSAGE 0 43
\END
\PRO 14
; hablar con Tabernero

_       _       PARSE
_       _       MES 0 52
_       HOLA    MESSAGE 0 28
                DONE
_       BEBIDA  PROCESS 15
                DONE
_       CUEVA   SYNONYM _ COSA
_       COSA    MESSAGE 0 53
                DONE
_       _       MESSAGE 0 43
\END
\PRO 15
; pedir bebida al tabernero

_       _       CARRIED 2       ; si tiene dinero suficiente (1 moneda)
                NOTEQ 58 0
                ISAT 11 252     ; y no tiene ya otra jarra de cerveza
                DEC 58
                MESSAGE 0 56
                CREATE 11       ; crea la jarra
                GET 11          ; y se la da
                DONE
_       _       CARRIED 2
                NOTEQ 58 0
                MESSAGE 0 60
                DONE
_       _       MESSAGE 0 55    ; si no tiene dinero
\END
\PRO 16
; hablar con Zora

_       _       PARSE
_       _       MES 0 61
_       HOLA    ZERO 59         ; si no ha hechado el pulso todav¡a
                MESSAGE 0 70    ; le invita a ello
                DONE
_       HOLA    MESSAGE 0 28
                DONE
_       YO      SYNONYM _ PULSO
ACEPTO  _       SYNONYM _ PULSO
_       SI      SYNONYM _ PULSO
_       PULSO   NOTZERO 59      ; si ya se hizo el pulso
                MESSAGE 0 71
                DONE
_       PULSO   CARRIED 2       ; si acepta el pulso y tiene dinero
                NOTEQ 58 0
                MESSAGE 0 62
                PROCESS 17      ; pulso entre PJ y Zora
                SET 59          ; indica que el pulso ya se hizo
                DONE
_       PULSO   MESSAGE 0 63
                DONE
_       CUEVA   SYNONYM _ COSA
_       COSA    ZERO 60         ; si no le regal¢ una perla
                MESSAGE 0 75
                DONE
_       COSA    MESSAGE 0 76    ; si le regal¢ la perla
                SET 61          ; indica que habl¢ con Zora sobre cosa
                DONE
_       _       MESSAGE 0 72
\END
\PRO 17
; pulso entre PJ y Zora

_       _       ANYKEY
                CLW 1
_       _       CHANCE 50       ; si probabilidad 50%
                INK 4
                MESSAGE 0 64    ; Zora va ganando
                SKIP $fase2
_       _       INK 3
                MESSAGE 0 65    ; si no, PJ va ganando
$fase2
_       _       PAUSE 50
                CHANCE 75       ; si probabilidad 75%
                INK 6
                MESSAGE 0 66    ; Zora va ganando
                SKIP $Zora_gana
_       _       INK 2
                MESSAGE 0 67    ; si no, PJ va ganando
                CHANCE 25       ; 25% de que Zora gane
                SKIP $Zora_gana
$PJ_gana
_       _       PAUSE 50
                INK 2
                MESSAGE 0 69    ; PJ gana el pulso...
                INK 15
                ADD 58 100      ; ...y las monedas de Zora
                NEWLINE
                DONE
$Zora_gana
_       _       CHANCE 25       ; 25% de que PJ gane
                SKIP $PJ_gana
_       _       PAUSE 50
                INK 4
                BRIGHT 1
                MESSAGE 0 68    ; Zora gana el pulso...
                INK 15
                DESTROY 2       ; ...y las monedas del PJ
                NEWLINE
                DONE
\END
\PRO 18
; imprime 'o.' o 'a.' dependiendo del g‚nero del objeto
; Entrada: variable 8 conteniendo n£mero del objeto

_       _       HASAT 2         ; si es femenino
                PRINTC 97       ; imprime 'a'
                SKIP $punto
_       _       PRINTC 111      ; imprime 'o'
$punto
_       _       PRINTC 46       ; imprime '.'
                PRINTC 32       ; y un espacio
\END
\PRO 19
; hablar con Guardias

_       _       PARSE
_       _       MES 0 80
_       HOLA    MESSAGE 0 28
                DONE
_       _       MESSAGE 0 43
\END
\PRO 20
; procesa las teclas de funci¢n del condacto INPUT

; asignamos a cada tecla de funci¢n una acci¢n
; acabamos cada entrada con NOTDONE para que despu‚s de
; ejecutar este proceso salte para analizar la sentencia l¢gica
; y no vuelva a pedir un INPUT, adem s ponemos a 1 la bandera 63
; para que salte el condacto PARSE del Proceso 2

_       _       EQ 12 TECLA_F1
                SYNONYM MIRAR _
                SET 63
                NOTDONE

_       _       EQ 12 TECLA_F2
                SYNONYM INVENTARIO _
                SET 63
                NOTDONE

_       _       EQ 12 TECLA_F3
                SYNONYM GRABAR _
                SET 63
                NOTDONE

_       _       EQ 12 TECLA_F4
                SYNONYM CARGAR _
                SET 63
                NOTDONE

_       _       EQ 12 TECLA_F5
                SYNONYM FIN _
                SET 63
                NOTDONE

_       _       EQ 12 TECLA_F10
                DEBUG 1         ; si pulsa F10 activa paso a paso
                DONE
\END
\PRO 21
; impresi¢n de gr ficos de localidades

_       _       LET 47 [0]
                WINDOW 3
                CLW 3
_       _       INC 1
                GRAPHIC 0 [1] 1
_       _       DEC 1
_       _       WINDOW [47]
\END
